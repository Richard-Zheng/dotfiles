# smart zdotdir
_zdir=${ZDOTDIR:-$HOME}
# cache folder for zcompdump
ZSH_CACHE="${XDG_CACHE_HOME:-$HOME/.cache}/zsh/"
if [[ ! -d $ZSH_CACHE ]]; then
  mkdir -p $ZSH_CACHE 
fi

# Bootstrap zinit
ZINIT_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/zinit.git"
[ ! -d $ZINIT_HOME ] && mkdir -p "$(dirname $ZINIT_HOME)"
[ ! -d $ZINIT_HOME/.git ] && git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
source "${ZINIT_HOME}/zinit.zsh"

# Prompt
zinit ice depth=1; zinit light romkatv/powerlevel10k
[[ ! -f $_zdir/.p10k.zsh ]] || source $_zdir/.p10k.zsh

zinit wait lucid light-mode for \
  atinit"ZINIT[COMPINIT_OPTS]='-d $ZSH_CACHE/.zcompdump-${ZSH_VERSION}'; zicompinit; zicdreplay;" \
    Aloxaf/fzf-tab \
  zdharma-continuum/fast-syntax-highlighting \
  atload"_zsh_autosuggest_start" \
    zsh-users/zsh-autosuggestions \
  blockf atpull'zinit creinstall -q .' \
    zsh-users/zsh-completions \
  bindmap"^R -> ^H" atinit"
    zstyle :history-search-multi-word page-size 10
    zstyle :history-search-multi-word highlight-color fg=red,bold
    zstyle :plugin:history-search-multi-word reset-prompt-protect 1
  " \
    zdharma/history-search-multi-word \
  none9632/zsh-sudo \
  djui/alias-tips \
  OMZL::clipboard.zsh \
  atload"autoload -U colors && colors" \
    OMZP::colored-man-pages

ZSH_TMUX_AUTOSTART=true
zinit lucid light-mode for \
  atclone"echo 'set -g default-terminal \$ZSH_TMUX_TERM
source-file \$ZSH_TMUX_CONFIG' > ./tmux.extra.conf
echo 'set -g default-terminal \$ZSH_TMUX_TERM' > ./tmux.only.conf" \
    OMZP::tmux

zinit snippet PZT::modules/pacman/init.zsh

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
# if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
#   source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
# fi

# For ICS PA
export NEMU_HOME=$HOME/ics2024/nemu
export AM_HOME=$HOME/ics2024/abstract-machine

# History file
HISTFILE=~/.zsh_history
HISTSIZE=1000
SAVEHIST=1000
## History command configuration
setopt extended_history       # record timestamp of command in HISTFILE
setopt hist_expire_dups_first # delete duplicates first when HISTFILE size exceeds HISTSIZE
setopt hist_ignore_dups       # ignore duplicated commands history list
setopt hist_ignore_space      # ignore commands that start with space
setopt hist_verify            # show command with history expansion to user before running it
setopt share_history          # share history between shells
# use OS fcntl file lock
setopt hist_fcntl_lock 2>/dev/null

unsetopt beep
bindkey -e
# don't have to spell out cd
setopt autocd
# cd dir stack
setopt auto_pushd
setopt pushd_ignore_dups
setopt pushd_minus
# allow comments in interactive mode
setopt interactive_comments
# use # ^ ~ in patterns for filename generation
setopt extended_glob
# completion list columns can have diff widths
setopt list_packed
# path/file name completion on -file=/sth arguments
setopt magic_equal_subst
# show option state when run 'setopt'
setopt ksh_option_print
# don't slow down background jobs
setopt no_bg_nice
# enable ^S/^Q to be keymapped
setopt noflowcontrol; stty -ixon

# Change Xft.dpi instantly
# Usage: dpi <number>
function dpi() {
    sed -i -E s/Xft.dpi:.\*/Xft.dpi:$1/ ~/.Xresources && xrdb ~/.Xresources && i3-msg restart
}
